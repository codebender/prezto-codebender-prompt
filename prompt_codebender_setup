#
# A theme based on steeef, paradox, &  theme
#  * RVM/Rbenv info shown on the right
#  * Git branch info on the left
#  * Single line prompt
#
# Authors:
#   codebender https://github.com/codebender
#   skwp       https://github.com/skwp
#   steeef     https://github.com/steeef
#   paradox460 https://github.com/paradox460

function prompt_codebender_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info on
    git-info
  fi

  # Get ruby information
  if (( $+functions[ruby-info] )); then
    ruby-info
  fi
}

function prompt_codebender_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  autoload -Uz add-zsh-hook

  add-zsh-hook precmd prompt_codebender_precmd

  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    __PROMPT_CODEBENDER_COLORS=(
      "%F{81}"  # turquoise
      "%F{166}" # orange
      "%F{135}" # purple
      "%F{161}" # magenta
      "%F{118}" # limegreen
      "%F{88}"  # red
      "%F{27}"  # blue
    )
  else
    __PROMPT_CODEBENDER_COLORS=(
      "%F{cyan}"
      "%F{yellow}"
      "%F{magenta}"
      "%F{red}"
      "%F{green}"
      "%F{red}"
      "%F{blue}"
    )
  fi

  # git
  zstyle ':prezto:module:git:info' verbose 'yes'

  #zstyle ':prezto:module:git:info:branch' format "${__PROMPT_CODEBENDER_COLORS[1]}%b%f"
  #zstyle ':prezto:module:git:info:added' format "${__PROMPT_CODEBENDER_COLORS[5]}●%f"
  #zstyle ':prezto:module:git:info:dirty' format "${__PROMPT_CODEBENDER_COLORS[4]}●%f"
  #zstyle ':prezto:module:git:info:deleted' format "${__PROMPT_CODEBENDER_COLORS[2]}●%f"
  #zstyle ':prezto:module:git:info:modified' format "${__PROMPT_CODEBENDER_COLORS[4]}●%f"
  #zstyle ':prezto:module:git:info:untracked' format "${__PROMPT_CODEBENDER_COLORS[3]}●%f"
  zstyle ':prezto:module:git:info:action' format " ${__PROMPT_CODEBENDER_COLORS[6]}! %s%f"
  zstyle ':prezto:module:git:info:added' format " ${__PROMPT_CODEBENDER_COLORS[5]}✚%f"
  zstyle ':prezto:module:git:info:dirty' format " ${__PROMPT_CODEBENDER_COLORS[4]}●%f"
  zstyle ':prezto:module:git:info:ahead' format " ${__PROMPT_CODEBENDER_COLORS[5]}⬆%f"
  zstyle ':prezto:module:git:info:behind' format " ${__PROMPT_CODEBENDER_COLORS[6]}⬇%f"
  zstyle ':prezto:module:git:info:branch' format "${__PROMPT_CODEBENDER_COLORS[1]}⭠ %b%f"
  zstyle ':prezto:module:git:info:commit' format " ${__PROMPT_CODEBENDER_COLORS[1]}➦ %.7c%f"
  zstyle ':prezto:module:git:info:deleted' format " ${__PROMPT_CODEBENDER_COLORS[6]}✖%f"
  zstyle ':prezto:module:git:info:modified' format " ${__PROMPT_CODEBENDER_COLORS[3]}✱%f"
  zstyle ':prezto:module:git:info:position' format " ${__PROMPT_CODEBENDER_COLORS[7]}%p%f"
  zstyle ':prezto:module:git:info:renamed' format " ${__PROMPT_CODEBENDER_COLORS[5]}➙%f"
  zstyle ':prezto:module:git:info:stashed' format " ${__PROMPT_CODEBENDER_COLORS[6]}s%f"
  zstyle ':prezto:module:git:info:unmerged' format " ${__PROMPT_CODEBENDER_COLORS[2]}═%f"
  zstyle ':prezto:module:git:info:untracked' format " ${__PROMPT_CODEBENDER_COLORS[7]}?%f"

  #zstyle ':prezto:module:git:info:keys' format 'prompt' '(%b%d%a%m%u)'
  zstyle ':prezto:module:git:info:keys' format \
    'prompt' '(%b%d%a%m%u%p)' \
    'status' ' %A%B%S%a%d%m%r%U%u'

  # ruby info (rvm, rbenv)
  zstyle ':prezto:module:ruby:info:version' format '[%v]'

  PROMPT="${__PROMPT_CODEBENDER_COLORS[3]}%n%f ${__PROMPT_CODEBENDER_COLORS[5]}%~%f "'$git_info[prompt]'"$ "
  RPROMPT='%F{blue}${ruby_info[version]}'
}

prompt_codebender_setup "$@"
